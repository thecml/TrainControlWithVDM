class RadioBlockCenter
types
	public MovementAuthorityMessage = <MovementAuthorityGranted> | <MovementAuthorityDenied>;

instance variables
	private responsibleTracks : set of Track;
	private availableRoutes : set of Interlocking`Route; -- from interlocking
	private designatedRoutes : set of Interlocking`Route; -- from trains
	private busyTracks : map Track to bool; -- from interlocking
	private movementAuthorites : map nat to seq of Track;
	private interlocking  : Interlocking;
	
	inv card availableRoutes < 2 or forall rt in set availableRoutes
		& Interlocking`InvNoDuplicateTrack(rt);

operations
	public RequestMovementAuthority: (Track) ==> MovementAuthorityMessage -- from train
	RequestMovementAuthority(tr) ==
		if(tr in set responsibleTracks and {tr} in set availableRoutes and busyTracks(tr) = false)
		then (
			if (interlocking.RequestToProceed({tr}) = <PROCEED_GRANTED>)
			then (
				designatedRoutes := designatedRoutes union {{tr}};
				return <MovementAuthorityGranted>;
			) else return <MovementAuthorityDenied>;
		) else return <MovementAuthorityDenied>;
	
	public SetRouteAsAvailable: Interlocking`Route ==> ()
	SetRouteAsAvailable(rt) ==
		let track in set rt
		in (
			busyTracks(track) := false;
			availableRoutes := availableRoutes union {{track}}
		);
	
	public GetResponsibleTracks: () ==> set of Track
	GetResponsibleTracks() ==
		return responsibleTracks;
	
	public RadioBlockCenter : set of Interlocking`Route  * set of Track * Interlocking
	  ==> RadioBlockCenter
	RadioBlockCenter(rts,trs,itl) ==
	atomic (
		availableRoutes := rts;
		responsibleTracks := trs;
		interlocking := itl;
		designatedRoutes := {};
		busyTracks := {|->};
		movementAuthorites := {|->};
	);
		
functions
	private IsTrackInSetOfTracks: Track * set of Track -> bool
	IsTrackInSetOfTracks(tr,trs) ==
		tr in set trs;

	private IsTrackInRoute: Track * Interlocking`Route -> bool
	IsTrackInRoute(tr, rt) ==
			forall rtt in set rt & tr <> rtt => tr.fromCoord <> rtt.fromCoord
				or tr.toCoord <> rtt.toCoord
		pre card rt > 0;

	private IsTrackOccupied: Track * map Track to bool -> bool
	IsTrackOccupied(tr,routemap) ==
		routemap(tr) = false
		pre tr in set dom routemap;
	
	private GetBusyTracks: map Track to bool -> set of Track
	GetBusyTracks(trmap) ==
		{tr | tr in set dom trmap & trmap(tr) = false}
		
end RadioBlockCenter